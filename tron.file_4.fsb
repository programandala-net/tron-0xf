\ Tron
\ A game written in ZX Spectrum's Abersoft Forth.

\ tron.file_4.fsb
\ This file is the 5th part of the source.
\ It contains the words to redefine the player keys.

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ Licencia/Permesilo/License: GPL 3

( Load )

2 10 THRU
RUNT \ load the next part of the source

( Keyboard: redefine keys )

  \ Max lenght of a player key description in any language:
11 CONSTANT /KEY-DESC

  \ Screen coordinates:
10 CONSTANT KEYS-LINE     \ top line of the redefine menu
 0 CONSTANT KEYS1-COL     \ column of player 1's keys
10 CONSTANT KEY-DESC-COL  \ column of the key description
20 CONSTANT KEYS2-COL     \ column of player 2's keys

: +KEY-DESC-COL  ( ca len line -- ca len line col' )
  \ Relative column of a key description,
  \ depending on its lenght; it's relative to
  \ the absolute column of the key descriptions.
  OVER /KEY-DESC SWAP -  ;

: (TYPE-KEY-DESC)  ( ca len line col -- )
  \ Print a key description, on the given line,
  \ col columns from the left margin of its region.
  KEY-DESC-COL + AT TYPE  ;

: |KEY-DESC|  ( ca len line -- )
  \ Print a key description, on the given line,
  \ centered on its region.
  +KEY-DESC-COL 2 / (TYPE-KEY-DESC)  ;

: |KEY-DESC  ( ca len line -- )
  \ Print a key description, on the given line,
  \ at the left of its region.
  0 (TYPE-KEY-DESC)  ;

: KEY-DESC|  ( ca len line -- )
  \ Print a key description, on the given line,
  \ at the right of its region.
  OVER KEY-DESC-COL SWAP - (TYPE-KEY-DESC)  ;
  
( Keyboard: redefine keys )

HERE: KEY-DESCS
  \  This table contains the cfa of the four texts used
  \  to describe the player keys. When executed, they
  \  return a string in the current language.
  ' "UP" CFA , ' "DOWN" CFA , ' "LEFT" CFA , ' "RIGHT" CFA ,

: KEY-DESC  ( n -- ca len )
  \ Get the n element from the `KEY-DESCS` table.
  CELLS KEY-DESCS + @ EXECUTE  ;
  
: KEY.  ( line col a -- )
  \ Print a key name.
  \ line col = screen coordinates
  KEY-NAME 2SWAP AT TYPE  ;

: PLAYER-KEY.  ( line col a n -- )
  \ Print a player key name.
  \ line col = screen coordinates
  \ a = player keys table
  \ n = key number in the table 
  CELLS + @ KEY.  ;

: REDEFINE-MENU  ( -- )
  \ Show the menu to redefine the keys.
  KEYS/PLAYER 0 DO
    I KEYS-LINE +
    DUP KEYS1-COL KEYS1 I PLAYER-KEY.
    DUP I KEY-DESC |KEY-DESC|
        KEYS2-COL KEYS2 I PLAYER-KEY.
  LOOP  ;

( Keyboard: redefine keys )

: REDEFINED?  ( -- f )
  \ Ask for confirmation: Are the redefined keys ok?
  \ XXX TODO
  TRUE  ;

: (REDEFINE)  ( a line col -- )
  \ Redefine the keys of a player.
  \ a = table of the player keys (`KEYS1` or `KEYS2`)
  \ line col = coordinate of the first key
  ROT KEYS/PLAYER BOUNDS DO
    2DUP  PRESSED DUP I ! KEY.
  LOOP  2DROP  ;

: REDEFINE1  ( -- )  KEYS1 (REDEFINE)  ;
: REDEFINE2  ( -- )  KEYS2 (REDEFINE)  ;

: REDEFINE  ( -- )
  \ Redefine the keys.
  REDEFINE-MENU
  BEGIN  REDEFINE1 REDEFINE2 REDEFINED?  UNTIL  ;

  \ vim: filetype=abersoftforth:fileencoding=utf-8

