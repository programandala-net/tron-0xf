\ Tron (version 201503241634)
\ A game written in ZX Spectrum's Abersoft Forth.

  \ tron.file_0.fsb
  \ This file is the 1st part of the source.

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ Licencia/Permesilo/License: http://programandala.net/lp

\ http://programandala.net/es.programa.tron.html

  \ -----------------------------------------------------------
  \ Requisitos

  \ Para convertir este fichero fuente en un fichero TAP
  \ utilizable por Abersoft Forth en un emulador de ZX Spectrum
  \ se necesita el conversor fsb:
  \
  \ http://programandala.net/es.programa.fsb.html

( Load)

CR ." Loading Tron..." CR

FORTH DEFINITIONS

2 LOAD  3 LOAD  4 LOAD  5 LOAD 
6 LOAD  7 LOAD  8 LOAD  9 LOAD  10 LOAD

RUNT \ Load the next part of the source

( Standard extensions: comments and strings)

: \  ( "ccc<newline>" -- )
  IN @ DUP C/L > IF  B/BUF SWAP MOD  ELSE  C/L SWAP - THEN
  IN +!  ; IMMEDIATE

: SLIT  ( -- ca len ) R COUNT DUP 1+ R> + >R  ;

: PARSE-TEXT  ( c "ccc<c>" -- ca len )  TEXT PAD COUNT  ;

: (S) ( Compilation: c "ccc<c>" -- ) ( Run-time:  -- ca len )
  STATE @
  IF    COMPILE SLIT WORD HERE C@ 1+ ALLOT
  ELSE  PARSE-TEXT  THEN  ;

: CHAR  ( "name" -- c )  BL WORD HERE 1+ C@  ;

: [CHAR]  ( "name" -- c )  CHAR [COMPILE] LITERAL  ; IMMEDIATE

: S"  ( Compilation: "ccc<">" -- ) ( Run-time:  -- ca len )
  [CHAR] " (S)  ; IMMEDIATE

( Standard extensions: other)

: THRU  ( n1 n2 -- )  1+ SWAP DO  I LOAD  LOOP  ;

  \ Abersoft Forth's `EXIT` just does a `RDROP`, so:
: RDROP  ( R: x -- )  EXIT  ;

  \ And this is what `EXIT` is supposed to do:
: EXIT  ( -- )  COMPILE [COMPILE] ;S  ; IMMEDIATE

: BYE MON  ;
  \ 'MON' could be actually renamed to 'BYE' this way:
    \ ' MON NFA 1+
    \ CHAR B OVER C!
    \ CHAR Y OVER 1+ C!
    \ CHAR E 128 + SWAP 2+ C!

2 CONSTANT CELL
: CELL+  ( n1 -- n2 )  2+  ;
: CELLS ( n1 -- n2 )  CELL *  ;

: BOUNDS  ( a1 len -- a2 a1 )  OVER + SWAP  ;

( Abersoft Forth specific extensions)


: TLOAD  ( n -- )
  \ Load the given block from the next file on the tape.
  FLUSH INIT-DISC LOADT LOAD  ;

: RUNT  ( -- )
  \ Load block 1 from the next file on the tape.
  1 TLOAD QUIT  ;

( ZX Spectrum specific)

  \ Memory addresses

22528 CONSTANT ATTRIBUTES

  \ System variables

23677 DUP CONSTANT X-COORD  1+ CONSTANT Y-COORD

  \ Color constants

0 CONSTANT BLACK   2 CONSTANT RED    5 CONSTANT CYAN
6 CONSTANT YELLOW  7 CONSTANT WHITE

: PAPERY  ( color -- paper-attribute )  8 *  ;

( Vocabulary, variables and constants)

  \ ----------------------------
  \ Game vocabulary

: TASK  ;

VOCABULARY TRON IMMEDIATE  TRON DEFINITIONS

  \ ----------------------------
  \ Game variables and constants

     \ Player 1            Player 2
 \   1 VARIABLE HUMAN1     0 VARIABLE HUMAN2  \ human? XXX TODO
     0 VARIABLE SCORE1     0 VARIABLE SCORE2  \ scores
     0 VARIABLE XC1        0 VARIABLE XC2     \ X coordinate
     0 VARIABLE YC1        0 VARIABLE YC2     \ Y coordinate
     0 VARIABLE XINC1      0 VARIABLE XINC2   \ X inc (-1..1)
     0 VARIABLE YINC1      0 VARIABLE YINC2   \ Y inc (-1..1)
YELLOW CONSTANT COLOR1  CYAN CONSTANT COLOR2  \ ink color

  \ Languages

 0 CONSTANT EN  \ English
 1 CONSTANT EO  \ Esperanto
 2 CONSTANT ES  \ Spanish
EN VARIABLE LANG

  \ ----------------------------
  \ Generic constants

  CHAR n CONSTANT 'n'
  CHAR s CONSTANT 's'

