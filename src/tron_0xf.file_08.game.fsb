\ tron_0xf.file_08.game.fsb

\ This file is part of
\ Tron 0xF
\ A ZX Spectrum game written in Abersoft Forth

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ Licencia/Permesilo/License: GPL 3

\ http://programandala.net/en.program.tron_0xf.html

.( Tron 0xF: game )

2 5 THRU
  
( Paint )

: PAINT  ( -- )
  CURRENT-COLOR @ INK CURRENT-XC @ CURRENT-YC @ PLOT  ;

( Crash effect )

: COORDS>CURSOR  ( x y -- line col )
  \ Convert graphic coordinates to line and column.
  175 SWAP - 8 / SWAP 8 /  ;

: (CRASH-NOISE)  ( -- )
  \ XXX TODO
  100 50 DO  11 I BLEEP  LOOP  ;

DEFER CRASH-NOISE  ( -- )  ' (CRASH-NOISE) IS CRASH-NOISE

  \ XXX TODO
: CRASH  ( -- )  CRASH-NOISE  ;

( Noise )

HERE: MOTOR-NOISES
  \ Table of `BLEEP` parameters for every player.
  0 1 , ,  100 1 , ,  200 1 , ,

: (MOTOR-NOISE)  ( -- )
  \ Do the motor noise of the current player.
  CURRENT-PLAYER [ 2 CELLS ] LITERAL * MOTOR-NOISES + 2@
  BLEEP  ;

DEFER MOTOR-NOISE  ' (MOTOR-NOISE) IS MOTOR-NOISE

: TOGGLE-SOUND  ( -- )

  \ Toggle the sound on and off, by changing what the deferred
  \ words `MOTOR-NOISE` and `CRASH-NOISE` execute.

  ' MOTOR-NOISE DEFER@ [ ' (MOTOR-NOISE) CFA ] LITERAL =
  IF    ' NOOP
  ELSE  ' (MOTOR-NOISE)  THEN  ' MOTOR-NOISE DEFER!

  ' CRASH-NOISE DEFER@ [ ' (CRASH-NOISE) CFA ] LITERAL =
  IF    ' NOOP
  ELSE  ' (CRASH-NOISE)  THEN  ' CRASH-NOISE DEFER!  ;

( Collisions )

999 CONSTANT MAX-SCORE

: SCORE!   ( n a -- )
  \ Update the score stored in 'a' with 'n';
  \ if the result is greater than the maximum score,
  \ reset the score to zero.
  DUP @ ROT + DUP MAX-SCORE > IF  0=  THEN  SWAP !  ;

: SCORE+  ( n -- )
  CURRENT-SCORE SCORE! .SCORE  ;

: CRASH?  ( -- f )  CURRENT-COORDS 2@ POINT  ;

: +SCORES  ( -- )
  \ Increase the score of the players that didn't crash.
  \ CRASH1? 0= SCORE1+  CRASH2? 0= SCORE2+  CRASH3? 0= SCORE3+  ;
  PLAYERS 0 DO  I CURRENT-PLAYER! CRASH? 0= SCORE+  LOOP  ;

  \ vim: filetype=abersoftforth:fileencoding=utf-8
