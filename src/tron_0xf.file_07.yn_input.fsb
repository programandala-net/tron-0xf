\ tron_0xf.file_07.yn_input.fsb

\ This file is part of
\ Tron 0xF
\ A game written in ZX Spectrum's Abersoft Forth

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ Licencia/Permesilo/License: GPL 3

\ http://programandala.net/en.program.tron_0xf.html

.( Tron 0xF: Y/N input )

: UPPER  ( c -- c' )
  \ Convert a char in the range a-z to uppercase.
  \ Warning: no check!, chars other than a-z get corrupted.
  223 AND  ;

: PAUSE  ( -- )
  \ Wait for a key press.
  CR PRESS-ANY-KEY$ |TYPE|  500 MS  AKEY DROP  ;

: Y/N?  ( c -- f )
  \ Is the given char a valid answer for a Y/N question?
  UPPER DUP 'Y' = SWAP 'N' = OR  ;

: Y/N  ( -- c )
  \ Wait for a valid key press for a Y/N question
  \ and return its code.
  BEGIN  AKEY DUP Y/N? 0=  WHILE  DROP  REPEAT  ;

: NO?  ( -- f )
  \ Wait for a valid key press for a Y/N question
  \ and return a flag: Was it "N" or "n"?
  Y/N UPPER 'N' =  ;


