\ tron_0xf.file_07.yn_input.fsb

\ This file is part of
\ Tron 0xF
\ A ZX Spectrum game written in Abersoft Forth

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ License: GPL 3

\ http://programandala.net/en.program.tron_0xf.html

.( Tron 0xF: Sound and Y/N input )

2 3 THRU

( Sound )

DEFER ?BLEEP  ( n1 n2 -- )

: SOUND  ( f -- )
  \ Turn the sound on or off, depending on the 'f' flag.
  IF  ' BLEEP ELSE  ' 2DROP  THEN  ' ?BLEEP DEFER!  ;

TRUE VARIABLE SOUND?  \ status of the sound, on or off
SOUND? @ SOUND        \ set default

: KEY-SOUND  ( -- )
  \ Sound used when a key is pressed, except during the game.
  \ XXX TODO
  100 50 DO  11 I ?BLEEP  LOOP  ;

( Y/N input )

: PRESS-ANY-KEY  ( -- )
  \ Wait for a key press.
  CR PRESS-ANY-KEY$ 23 |LINETYPE|
  500 MS  AKEY DROP KEY-SOUND  ;

: Y/N?  ( c -- f )
  \ Is the given char a valid answer for a Y/N question?
  UPPERC DUP 'Y' = SWAP 'N' = OR  ;

: Y/N  ( -- c )
  \ Wait for a valid key press for a Y/N question
  \ and return its code.
  BEGIN  AKEY DUP Y/N? 0=  WHILE  DROP  REPEAT  KEY-SOUND  ;

: NO?  ( -- f )
  \ Wait for a valid key press for a Y/N question
  \ and return a flag: Was it "N" or "n"?
  Y/N UPPERC 'N' =  ;

  \ vim: filetype=abersoftforth:fileencoding=utf-8
