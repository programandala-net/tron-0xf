\ tron_0xf.file_11.game.fsb

\ This file is part of
\ Tron 0xF
\ A ZX Spectrum game written in fig-Forth with Abersoft Forth

\ http://programandala.net/en.program.tron_0xf.html

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ License: GPL 3

  \ Tron 0xF is free software; you can redistribute it
  \ and/or modify it under the terms of the GNU General Public
  \ License as published by the Free Software Foundation;
  \ either version 3 of the License, or (at your option) any
  \ later version.
  \
  \ Tron 0xF is distributed in the hope that it will be
  \ useful, but WITHOUT ANY WARRANTY; without even the implied
  \ warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
  \ PURPOSE.  See the GNU General Public License for more
  \ details.
  \
  \ You should have received a copy of the GNU General Public
  \ License along with Tron 0xF; if not, see
  \ <http://gnu.org/licenses>.

.( Tron 0xF: game )

2 10 THRU

( Rudder increments)

 \ Coordinate increments.
 \ Double constants are used to update the coordinate
 \ increments faster.

 \ X  Y 
   0  1 2CONSTANT UP-INCS
   0 -1 2CONSTANT DOWN-INCS
  -1  0 2CONSTANT LEFT-INCS
   1  0 2CONSTANT RIGHT-INCS

  \ XXX OLD -- first version:
  \ : -1|1  ( -- -1 | 1 )
  \   \ Return a random increment for a graphic coordinate.
  \   SYS-FRAMES C@ 1 AND DUP ?EXIT 1-  ;

  \ XXX NEW -- faster version in Z80:
CREATE -1|1  ( -- -1 | 1 )
  \ Return a random increment for a graphic coordinate.
  HEX
  3A C, 5C78 ,        \ ld a,(FRAMES)
  E6 C, 01 C,         \ and 1
  26 C, 00 C,         \ ld h,0
  68 07 + C,          \ ld l,a
  C2 C, PUSHHL ,      \ jp nz,PUSHHL ; return 1
  2B C,               \ dec hl
  C3 C, PUSHHL ,      \ jp PUSHHL ; return -1
  SMUDGE DECIMAL

( INCS2* )

CREATE INCS2*  ( n1 n2 -- n1' n2' )  HEX
  \ Double the coordinate increments b1 and b2
  \ by shifting left their low bytes directly on the stack.
  \ n1' = n1 2*
  \ n2' = n2 2*
  21 C, 0 ,       \ ld hl,0
  39 C,           \ add hl,sp ; low byte of TOS
  CB C, 26 C,     \ sla (hl)
  23 C,           \ inc hl ; high byte of TOS
  23 C,           \ inc hl ; low byte of 2TOS
  CB C, 26 C,     \ sla (hl)
  C3 C, NEXT ,    \ jp NEXT
  SMUDGE DECIMAL

( AUTOPILOT )

: CRASHING?  ( xinc yinc -- f )
  \ Considering the given coordinate increments
  \ for the current player,
  \ is the second next position occupied?
  INCS2* PLAYER-YC @ + SWAP PLAYER-XC @ + SWAP POINT  ;
  \ XXX TODO check if this is slower:
  \ INCS2* PLAYER-YC @ + >R PLAYER-XC @ + R> POINT  ;

: DODGED?  ( xinc yinc -- f )
  \ Considering the given coordinate increments
  \ for the current player,
  \ is the second next position free?
  CRASHING? 0=  ;

: DODGE-Y  ( -- 0 yinc )
  \ Choose increments to start moving on the Y axis.
  0 -1|1 2DUP DODGED? ?EXIT  MINUS  ;

: DODGE-X  ( -- xinc 0 )
  \ Choose increments to start moving on the X axis.
  -1|1 0 2DUP DODGED? ?EXIT  SWAP MINUS SWAP  ;

: DODGE  ( -- )
  \ Change the direction of the current player.
  PLAYER-XINC @   \ moving on the X axis?
  IF  DODGE-Y  ELSE  DODGE-X  THEN  PLAYER-INCS 2!  ;

: AUTOPILOT  ( -- )
  \ Update the coordinate increments
  \ of the current (robot) player:
  \ If there's something ahead, change the direction.
  PLAYER-INCS 2@ CRASHING?  IF  DODGE  THEN  ;

( RUDDER )

: RUDDER  ( -- )
  \ Update the coordinate increments
  \ of the current (human) player.
  PLAYER-UP-K @ PRESSED? IF
    PLAYER-XINC @ IF  UP-INCS PLAYER-INCS 2!  THEN  EXIT
  THEN
  PLAYER-DOWN-K @ PRESSED? IF
    PLAYER-XINC @ IF  DOWN-INCS PLAYER-INCS 2!  THEN  EXIT
  THEN
  PLAYER-LEFT-K @ PRESSED? IF
    PLAYER-YINC @ IF  LEFT-INCS PLAYER-INCS 2!  THEN  EXIT
  THEN
  PLAYER-RIGHT-K @ PRESSED? IF
    PLAYER-YINC @ IF  RIGHT-INCS PLAYER-INCS 2!  THEN  EXIT
  THEN  ;

( POSITIONS READY )

: POSITIONS1  ( -- )
  \ Set the player's position before a new round,
  \ for one player.
  127 XC0 ! 88 YC0 !  UP-INCS INCS0 2!  ;

: POSITIONS2  ( -- )
  \ Set the players' positions before a new round,
  \ for two players.
   40 XC0 ! 88 YC0 !  RIGHT-INCS INCS0 2!
  215 XC1 ! 88 YC1 !  LEFT-INCS  INCS1 2!  ;

: POSITIONS3  ( -- )
  \ Set the players' positions before a new round,
  \ for three players.
   40 XC0 ! 127 YC0 !  RIGHT-INCS INCS0 2!
  215 XC1 ! 127 YC1 !  LEFT-INCS  INCS1 2!
  127 XC2 !  40 YC2 !  UP-INCS    INCS2 2!  ;

HERE: POSITIONS-TABLE
  \ Execution table for `POSITIONS`.
  ' POSITIONS1 CFA , ' POSITIONS2 CFA , ' POSITIONS3 CFA ,
 
: POSITIONS  ( -- )
  \ Set the players' start positions.
  PLAYERS 1- CELLS POSITIONS-TABLE + @ EXECUTE  ;

: READY  ( -- )
  \ Show all players at their start positions.
  POSITIONS  PLAYERS 0 DO  I PLAYER! PAINT  LOOP  ;

( ROUND )

  \ Init a game round.
  \ XXX TODO message
: ROUND-INIT  ( -- )  CLEAR-ARENA READY AKEY DROP  ;

: FORWARD  ( -- )
  \ Update the coordinates of the current player.
  PLAYER-XINC @ PLAYER-XC +!  PLAYER-YINC @ PLAYER-YC +!  ;

: BREAK?  ( -- f )
  FALSE  ?TERMINAL 0= ?EXIT  DROP SAVE-DISPLAY DO-STOP?
  DUP 0= IF  RESTORE-DISPLAY  THEN  ;

: ROUND  ( -- f )
  \ Game round, until a crash or the break key is pressed.
  \ f = Break key pressed?
  ROUND-INIT
  BEGIN
    PLAYERS 0 DO
      I PLAYER!  PLAYER-CONTROL @ EXECUTE
      CRASH? IF  CRASH +SCORES UNLOOP FALSE EXIT  THEN
      FORWARD PAINT MOTOR-NOISE
    LOOP
  BREAK? UNTIL  TRUE  ;

( GAME )

  \ : ENOUGH?  ( -- f )  \ XXX OLD -- not used
  \   \ No more rounds?
  \   WHITE INK  ANOTHER?$ 12 |LINETYPE|  NO?  ;

: -SCORES  ( -- )
  \ Reset all scores.
  PLAYERS 0 DO  I PLAYER!  PLAYER-SCORE OFF  LOOP  ;

: GAME-INIT  ( -- )
  \ Init the game.
  CLS0 -SCORES ARENA SAVE-ARENA  SOUND? @ SOUND  ;

: ?ROUND-END  ( f -- )
  \ XXX TMP
  ?EXIT  AKEY DROP  ;

: GAME  ( -- )
  \ Game loop.
  GAME-INIT BEGIN  ROUND DUP 0= ?ROUND-END  UNTIL  PAGE   ;

  \ Init the players' controls. By default, only player 0 is
  \ human. Their type (human or robot) is already set in the
  \ data module, but their controls have to be init here,
  \ when `RUDDER` and `AUTOPILOT` have been defined.

   ' RUDDER CFA CONTROL0 !
' AUTOPILOT CFA CONTROL1 !
' AUTOPILOT CFA CONTROL2 !

  \ vim: filetype=abersoftforth
