\ tron_0xf.file_06.localization.fsb

\ This file is part of
\ Tron 0xF
\ A game written in ZX Spectrum's Abersoft Forth

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ Licencia/Permesilo/License: GPL 3

\ http://programandala.net/en.program.tron_0xf.html

  \ -----------------------------------------------------------
  \ fsb directives

  \ The following '#vim' directives, processed by fsb,
  \ translate the UTF-8 chars in this source file to ZX
  \ Spectrum UDG.

  \ #vim %substitute@á@\=nr2char(145)@g
  \ #vim %substitute@é@\=nr2char(147)@g
  \ #vim %substitute@í@\=nr2char(149)@g
  \ #vim %substitute@ó@\=nr2char(151)@g
  \ #vim %substitute@ú@\=nr2char(153)@g
  \ #vim %substitute@ñ@\=nr2char(157)@g
  \ #vim %substitute@¿@\=nr2char(158)@g
 
  \ #vim %substitute@Ĉ@\=nr2char(144)@g
  \ #vim %substitute@ĝ@\=nr2char(147)@g
  \ #vim %substitute@ŭ@\=nr2char(155)@g

.( Tron 0xF: localization )

2 10 THRU

( LOCALIZED )

: LOCALIZED  ( cfa1 cfa2 cfa3 "name" -- )

  \ Create a word that will execute another word from a list of
  \ three, depending on the current language.

  \ Parameters are the parameter field addresses of the
  \ localized versions, in reverse order of ISO language code:
  \ pfa1 = pfa of the Spanish word (ES)
  \ pfa2 = pfa of the Esperanto word (EO)
  \ pfa3 = pfa of the English word (EN)

  \ First, create a new word in the current dictionary with the
  \ given name, and store the three cfa (cofe field
  \ addresses) of the given words into its own pfa:

  <BUILDS CFA , CFA , CFA ,

  \ Second, define what the new word will do when executed:
  \ fetch from its pfa one of the three stored cfa depending on
  \ the current language, and execute it:

  DOES>  ( pfa )  LANG @ CELLS + @ EXECUTE  ;

( Localized menu options)

' ES-OPTION-0$ ' EO-OPTION-0$ ' EN-OPTION-0$
  LOCALIZED OPTION-0$  ( -- ca len )
' ES-OPTION-1$ ' EO-OPTION-1$ ' EN-OPTION-1$
  LOCALIZED OPTION-1$  ( -- ca len )
' ES-OPTION-2$ ' EO-OPTION-2$ ' EN-OPTION-2$
  LOCALIZED OPTION-2$  ( -- ca len )
' ES-OPTION-3$ ' EO-OPTION-3$ ' EN-OPTION-3$
  LOCALIZED OPTION-3$  ( -- ca len )
' ES-OPTION-4$ ' EO-OPTION-4$ ' EN-OPTION-4$
  LOCALIZED OPTION-4$  ( -- ca len )

( Localized menu options)

' ES-OPTION-5$ ' EO-OPTION-5$ ' EN-OPTION-5$
  LOCALIZED OPTION-5$  ( -- ca len )
' ES-OPTION-6$ ' EO-OPTION-6$ ' EN-OPTION-6$
  LOCALIZED OPTION-6$  ( -- ca len )
' ES-OPTION-7$ ' EO-OPTION-7$ ' EN-OPTION-7$
  LOCALIZED OPTION-7$  ( -- ca len )

( Localized texts)

' ES-INSTRUCTIONS ' EO-INSTRUCTIONS ' EN-INSTRUCTIONS
LOCALIZED (INSTRUCTIONS)  ( -- )

' ES-UP$ ' EO-UP$ ' EN-UP$
  LOCALIZED UP$  ( -- ca len )
' ES-DOWN$ ' EO-DOWN$ ' EN-DOWN$
  LOCALIZED DOWN$  ( -- ca len )
' ES-LEFT$ ' EO-LEFT$ ' EN-LEFT$
  LOCALIZED LEFT$  ( -- ca len )
' ES-RIGHT$ ' EO-RIGHT$ ' EN-RIGHT$
  LOCALIZED RIGHT$  ( -- ca len )

' ES-PRESS-ANY-KEY$ ' EO-PRESS-ANY-KEY$ ' EN-PRESS-ANY-KEY$
LOCALIZED PRESS-ANY-KEY$  ( -- ca len )

' ES-LICENSE$ ' EO-LICENSE$ ' EN-LICENSE$
LOCALIZED LICENSE$  ( -- ca len )

( Localized texts) 

  \ XXX OLD
  \ ' ES-ANOTHER?$ ' EO-ANOTHER?$ ' EN-ANOTHER?$
  \ LOCALIZED ANOTHER?$  ( -- ca len )

' ES-'Y' ' EO-'Y' ' EN-'Y' LOCALIZED 'Y'  ( -- c )

' ES-QUIT-MESSAGE ' EO-QUIT-MESSAGE ' EN-QUIT-MESSAGE
LOCALIZED QUIT-MESSAGE  ( -- )

' ES-HUMAN$ ' EO-HUMAN$ ' EN-HUMAN$ LOCALIZED HUMAN$  ( -- )

' ES-ROBOT$ ' EO-ROBOT$ ' EN-ROBOT$ LOCALIZED ROBOT$  ( -- )

' ES-ON$ ' EO-ON$ ' EN-ON$ LOCALIZED ON$  ( -- )

' ES-OFF$ ' EO-OFF$ ' EN-OFF$ LOCALIZED OFF$  ( -- )

( Special texts and texts that don't need translation)

: ON/OFF$  ( f -- ca len )
  IF  ON$  ELSE  OFF$  THEN  ;
  
: CREDITS  ( -- )
  \  <------------------------------->
  ." Copyright (C) 1985,2009,2015" CR
  ." Marcos Cruz (programandala.net)" CR
  LICENSE$ TYPE ."  GPL 3" CR CR  ;
  \  <------------------------------->

CHAR N CONSTANT 'N'

  \ vim: filetype=abersoftforth:fileencoding=utf-8

