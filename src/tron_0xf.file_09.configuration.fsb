\ tron_0xf.file_09.configuration.fsb

\ This file is part of
\ Tron 0xF
\ A ZX Spectrum game written in Abersoft Forth

\ Copyright (C) 1985,2009,2015 Marcos Cruz (programandala.net)
\ Licencia/Permesilo/License: GPL 3

\ http://programandala.net/en.program.tron_0xf.html

.( Tron 0xF: configuration )

  \ Max length of a player key description in any language:
11 CONSTANT /KEY-DESC

  \ Screen coordinates:
10 CONSTANT KEYS-LINE     \ top line of the redefine menu
 0 CONSTANT KEY-DESC-COL  \ column of the key description

2 10 THRU

( Key descriptions )

: +KEY-DESC-COL  ( ca len line -- ca len line col' )
  \ Relative column of a key description,
  \ depending on its length; it's relative to
  \ the absolute column of the key descriptions.
  OVER /KEY-DESC SWAP -  ;

: (TYPE-KEY-DESC)  ( ca len line col -- )
  \ Print a key description, on the given line,
  \ col columns from the left margin of its region.
  KEY-DESC-COL + AT TYPE  ;

: |KEY-DESC|  ( ca len line -- )
  \ XXX OLD -- not used
  \ Print a key description, on the given line,
  \ centered on its region.
  +KEY-DESC-COL 2 / (TYPE-KEY-DESC)  ;

: |KEY-DESC  ( ca len line -- )
  \ Print a key description, on the given line,
  \ at the left of its region.
  0 (TYPE-KEY-DESC)  ;

: KEY-DESC|  ( ca len line -- )
  \ XXX OLD -- not used
  \ Print a key description, on the given line,
  \ at the right of its region.
  OVER KEY-DESC-COL SWAP - (TYPE-KEY-DESC)  ;
  
( Key descriptions )

HERE: KEY-DESCS
  \  This table contains the cfa of the four texts used
  \  to describe the player keys. When executed, they
  \  return a string in the current language.
  ' UP$ CFA , ' DOWN$ CFA , ' LEFT$ CFA , ' RIGHT$ CFA ,

: KEY-DESC  ( n -- ca len )
  \ Get the n element from the `KEY-DESCS` table.
  CELLS KEY-DESCS + @ EXECUTE  ;
  
: KEY.  ( line col a -- )
  \ Print a key name.
  \ a = key address in the keys table
  \ line col = screen coordinates
  \ 0 0 AT .S  \ XXX INFORMER
  KEY-NAME 2SWAP AT TYPE  ;

: PLAYER-KEY.  ( line col a n -- )
  \ Print a player key name.
  \ line col = screen coordinates
  \ a = player keys table
  \ n = key number in the table 
  CELLS + @ KEY.  ;

( Redefine keys )

HERE: KEYS-COL-TABLE
  \ Table of keys columns for every player.
  8 C, 18 C, 28 C,

: KEYS-COL  ( -- column )
  \ Keys column of the current player.
  PLAYER KEYS-COL-TABLE + C@  ;

: KEY#>COORDS  ( n -- line col )
  \ Convert a key number (0..3) of the current player
  \ to the key's cursor coordinates in the redefine menu.
  KEYS-LINE + KEYS-COL ;

: KEY-PROMPT  ( a n -- )
  \ a = player keys table
  \ n = key number in the table 
  DUP KEY#>COORDS 2SWAP 1 FLASH PLAYER-KEY. 0 FLASH  ;

: (REDEFINE)  ( -- f )
  \ Redefine the keys of the current player.
  \ f = quit redefining?
  FALSE  \ default return flag
  KEYS/PLAYER 0 DO
    PLAYER-KEYS I KEY-PROMPT  1000 MS PRESSED  ( a | ff )
    -DUP 0= IF  UNLOOP 0= EXIT  THEN
    DUP PLAYER-KEYS I CELLS + !  I KEY#>COORDS ROT KEY.
  LOOP  ;

( Redefine keys menu )

  \ XXX TODO faster; less factoring; no "player" on every
  \ column, just the number.

: PLAYER#$  ( -- ca len )
  \ The word "Player" in the current language,
  \ with the current player number.
  PLAYER$ PLAYER 1+ S->D <# #S #> S+  ;

: .PLAYER  ( -- )
  \ Print the current player, centered over the keys column.
  PLAYER#$ KEYS-COL OVER 2 / - KEYS-LINE 2- SWAP AT TYPE  ;

: .PLAYER#  ( -- )
  \ Print the current player.
  KEYS-LINE 2- KEYS-COL AT PLAYER 1+ .  ;

0 VALUE KEY#  \ local

: REDEFINE-MENU-KEY  ( n -- )
  \ Show a key of the redefine keys menu, for the 'n' player.
  \ XXX TODO do not call `.PLAYER#` for every key
  PLAYER!  .PLAYER#
  KEYS-LINE KEY# + KEYS-COL PLAYER-KEYS KEY# PLAYER-KEY.  ;

: REDEFINE-MENU-ROW  ( n -- )
  \ Show a row of the redefine keys menu, for the 'n' key.
  TO KEY#
  KEY# KEY-DESC KEYS-LINE KEY# + |KEY-DESC
  PLAYERS 0 DO  I REDEFINE-MENU-KEY  LOOP  ;

: REDEFINE-MENU  ( -- )
  \ Show the redefine keys menu.
  CLS  KEYS/PLAYER 0 DO  I REDEFINE-MENU-ROW  LOOP  ;

( Redefine keys )

: REDEFINE  ( -- )
  \ Redefine the keys of all players.
  PLAYERS 0 DO
    I PLAYER! (REDEFINE) IF  LEAVE  THEN
  LOOP  ;

: REDEFINED?  ( -- f )
  \ Ask for confirmation: Are the redefined keys ok?
  \ XXX TODO
  TRUE  ;

: CONFIGURATION  ( -- )
  \ Redefine the keys.
  \ XXX TODO -- human/machine options
  REDEFINE-MENU
  BEGIN  REDEFINE REDEFINED?  UNTIL  ;

  \ vim: filetype=abersoftforth:fileencoding=utf-8

